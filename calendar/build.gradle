plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

group 'com.podobong.calendar'
version '1.0.0'

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact androidSourcesJar
            artifact "$buildDir/outputs/aar/calendar-release.aar"
            groupId 'com.podobong.calendar'
            artifactId 'calendar'
            version '1.0.0'
            pom {
                name = 'Calendar'
                description = 'Custom CalendarView for Android'
                url = 'https://github.com/podobongbong/bong-calendar-android'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'podobongbong'
                        name = 'Kim Hyein'
                        email = 'podobongbong@kakao.com'
                    }
                }
                scm {
                    connection = 'https://github.com/podobongbong/bong-calendar-android.git'
                    developerConnection = 'https://github.com/podobongbong/bong-calendar-android.git'
                    url = 'https://github.com/podobongbong/bong-calendar-android'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://api.bintray.com/maven/podobongbong/android/calendar/;publish=1'
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apiKey')

    publications = ['mavenJava']
    configurations = ['archives']
    pkg {
        repo = properties.getProperty('bintray.repoName')
        name = 'calendar'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/podobongbong/bong-calendar-android.git'
        publicDownloadNumbers = true
        publish = true
        version {
            name = '1.0.0'
            desc = '1.0.0'
            vcsTag = '1.0.0'
            gpg {
                sign = true
            }
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}